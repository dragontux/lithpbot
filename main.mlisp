#!/usr/bin/env gojira

import! 'lists

; todo: move this to seperate configuration file
define config : hashmap
    . :server   "irc.rizon.net"
    . :port     6667
    . :nick     "cpt_ahab"
    . :channels '["#cpt_ahab"]

map load! ' : "misc.scm"
            . "irc.scm"
            . "manage.scm"

print "hello, world!"

print "[ ] Starting irc bot."

define server : irc-connect config

begin
    [server :user] : config :nick
    [server :nick] : config :nick
    print "[ ] Connected to server."
    display "[ ] Have server: "
    print server

[server :privmsg] "NickServ"
       string-append "identify " : readall : open "./passfile" "r"

foreach [iota 5] intern-sleep

foreach [config :channels] : func [chan]
    display "[ ] Joining "
    print chan
    [server :join] chan

define parse-irc-message : func [msg]
     define split : list-split msg #\ 
     hashmap
       . :channel : list-ref split 2
       . :message : after (after msg #\:) #\:
       . :who     : car split
       . :action  : cadr split
       . :nick    : after (delim (car split) #\!) #\:
       . :host    : after (car split) #\@

define irc-field : func [msg val] : list->string [msg val]

define irc-replyto : func [msg]
    if [eq? (car (msg :channel)) #\#]
      list->string [msg :channel]
      list->string [msg :nick]

define str-concat : func [xs]
    if (null? xs)
      . ""
      string-append (car xs) : str-concat (cdr xs)

define command-list
  list 
    list ".bots"         : func [msg]
                            [server :privmsg] [irc-replyto msg]
                                . "Reporting in! [4Scheme] try ,help"

    list ",source"       : func [msg]
                            [server :privmsg] (irc-replyto msg)
                                . "[todo] insert source link here"

    list ",help"         : func [msg]
                            [server :privmsg] (irc-replyto msg)
                                str-concat : list (irc-field msg :nick)
                                    . ": sorry, I don't have a help command at the moment..."

    list ",whoami"       : func [msg]
                            [server :privmsg] (irc-replyto msg)
                                string-append "Hey there, your host is " : list->string : msg :host

    list ",maw"          : func [msg]
                            [server :privmsg] (irc-replyto msg)
                                str-concat : list (irc-field msg :nick) ": marf \\(^~^ )7"

    list ",manage"         bot-manage

    list "VERSION"   : func [msg]
                            [server :notice] (irc-field msg :nick)
                                . "VERSION I'm an irc bot"

define filter : func [xs sieve]
    if (null? xs)
        . '()
        if (sieve xs)
            cons (car xs) : filter (cdr xs) sieve
            filter (cdr xs) sieve

define ping? : func [xs] : equal? (take xs 4) (str-iter "PING")

foreach [[server :loop] :iter] : func [n]
    define message : until #\return tcp-getchar : server :socket
    tcp-getchar : server :socket ; just ignore the newline character
    map display : list "message " n ": "

    if (ping? message)
        begin
            print "Got ping"
            [server :rawmsg] : list->string : list-replace message #\I #\O

        begin
            define msg [parse-irc-message message]
            define split : map list->string : list-split [msg :message] #\ 

            foreach command-list : func [cmd]
                if (infix [first cmd] = [first split])
                   [cadr cmd] msg
                   . '()

            print : [parse-irc-message message] :action
